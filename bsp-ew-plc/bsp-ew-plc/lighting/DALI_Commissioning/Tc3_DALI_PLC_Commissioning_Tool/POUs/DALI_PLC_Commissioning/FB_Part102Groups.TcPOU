<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.10">
  <POU Name="FB_Part102Groups" Id="{f7ea4ad8-faaa-4a78-a3e5-0bb651dcbbef}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Part102Groups
VAR_INPUT
	ipDALICommunication				:	I_DALICommunication;
END_VAR
VAR_OUTPUT
	bBusy							:	BOOL;
END_VAR
VAR
	{region "Visu"}
	nGroupAddress					:	BYTE;
	bRead							:	BOOL;
	bWrite   						:	BOOL;
	bSwitchOn						:	BOOL;
	bSwitchOff   					:	BOOL;
	aShowAlarmState					:	ARRAY [0..63] OF BOOL;
	aActivateButton					:	ARRAY [0..63] OF BOOL;
	aButtonClick					:	ARRAY [0..63] OF BOOL;
	{endregion}
	i								:	INT;
	bHideWaitSymbol					:	BOOL := TRUE;
	nStep							:	INT;
	nAddress						:	BYTE;
	nNewAddress						:	BYTE;
	fbRecallMaxLevel				:	FB_DALI102RecallMaxLevel(0);
	fbOff							:	FB_DALI102Off(0);
	fb102QueryGroups				:	FB_DALI102QueryGroups(0);
	fb102AddToGroup					:	FB_DALI102AddToGroup(0);
	fb102RemoveFromGroup			:	FB_DALI102RemoveFromGroup(0);
	aDeviceGroupState				:	ARRAY [0..63] OF BYTE; // 0: device isn't present / 1: device belongs to the group / 2: device is not part of the group
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[{region "Detect Commands"}
IF (bSwitchOn AND (nStep = 0)) THEN
	nStep := 10;
	bSwitchOn := FALSE;
END_IF

IF (bSwitchOff AND (nStep = 0)) THEN
	nStep := 20;
	bSwitchOff := FALSE;
END_IF

IF (bRead AND (nStep = 0)) THEN
	nStep := 1000;
	bRead := FALSE;
	bHideWaitSymbol := FALSE;
	nAddress := 0;
END_IF

IF (bWrite AND (nStep = 0)) THEN
	bWrite := FALSE;
	bHideWaitSymbol := FALSE;
	FOR i := 0 TO 63 DO
		IF (aDeviceGroupState[i] <> 0) THEN
			nAddress := TO_BYTE(i);
			nStep := 2000;
			EXIT;
		 END_IF
	END_FOR
END_IF

IF (nStep = 0) THEN
	FOR i := 0 TO 63 DO
		IF aButtonClick[i] THEN
			aButtonClick[i] := FALSE;
			CASE aDeviceGroupState[i] OF
			1:	// remove device from group
				aActivateButton[i] := TRUE;
				aShowAlarmState[i] := FALSE;
				aDeviceGroupState[i] := 2;
			2:	// add device to group
				aActivateButton[i] := TRUE;
				aShowAlarmState[i] := TRUE;
				aDeviceGroupState[i] := 1;
			ELSE
				aActivateButton[i] := FALSE;
				aShowAlarmState[i] := FALSE;
				aDeviceGroupState[i] := 0;
			END_CASE
		END_IF 
	END_FOR
END_IF
{endregion}
{region "Manage Dialog Selection}
IF (nStep = 0) THEN
	bBusy := FALSE;
ELSE
	bBusy := TRUE;
END_IF
{endregion}
{region "Execute Commands"}
CASE nStep OF
0:
{region "Init"}
	bSwitchOn := FALSE;
	bSwitchOff := FALSE;
	bRead := FALSE;
	bWrite := FALSE;
	bHideWaitSymbol := TRUE;

	fbRecallMaxLevel.ipDALICommunication := ipDALICommunication;
	fbOff.ipDALICommunication := ipDALICommunication;
	fb102QueryGroups.ipDALICommunication := ipDALICommunication;
	fb102AddToGroup.ipDALICommunication := ipDALICommunication;
	fb102RemoveFromGroup.ipDALICommunication := ipDALICommunication;
{endregion}
10:
{region "Recall Max Level"}
	fbRecallMaxLevel(bStart := TRUE,
					 nAddress := nGroupAddress,
					 eAddressType := E_DALIAddressType.Group,
					 eCommandPriority := E_DALICommandPriority.Middle);
	IF (NOT fbRecallMaxLevel.bBusy) THEN
		fbRecallMaxLevel(bStart := FALSE);
		nStep := 0;
	END_IF
{endregion}
20:
{region "Off"}
	fbOff(bStart := TRUE,
		  nAddress := nGroupAddress,
		  eAddressType := E_DALIAddressType.Group,
		  eCommandPriority := E_DALICommandPriority.Middle);
	IF (NOT fbOff.bBusy) THEN
		fbOff(bStart := FALSE);
		nStep := 0;
	END_IF
{endregion}
1000:
{region "Read"}
	fb102QueryGroups(bStart := TRUE,
					 nAddress := nAddress,
					 eAddressType := E_DALIAddressType.Short,
					 eCommandPriority := E_DALICommandPriority.Middle);
	IF (NOT fb102QueryGroups.bBusy) THEN
		IF (NOT fb102QueryGroups.bError) THEN
			IF ((fb102QueryGroups.nGroups AND SHL(16#0001, TO_WORD(nGroupAddress))) <> 0) THEN
				aDeviceGroupState[nAddress] := 1;
			ELSE
				aDeviceGroupState[nAddress] := 2;
			END_IF
		ELSE
			aDeviceGroupState[nAddress] := 0;
		END_IF
		fb102QueryGroups(bStart := FALSE);
		nAddress := nAddress + 1;
		IF (nAddress > 63) THEN
			FOR i := 0 TO 63 DO
				CASE aDeviceGroupState[i] OF
				0:	// device isn't present
					aActivateButton[i] := FALSE;
					aShowAlarmState[i] := FALSE;
				1:	// device belongs to the group
					aActivateButton[i] := TRUE;
					aShowAlarmState[i] := TRUE;
				2:	// device is not part of the group
					aActivateButton[i] := TRUE;
					aShowAlarmState[i] := FALSE;
				ELSE
					aActivateButton[i] := FALSE;
					aShowAlarmState[i] := FALSE;
				END_CASE
			END_FOR
			nStep := 0;
		END_IF
	END_IF
{endregion}
2000:
{region "Write"}
	IF (aDeviceGroupState[nAddress] = 1) THEN
		// add to group
		fb102AddToGroup(bStart := TRUE,
						nAddress := nAddress,
						eAddressType := E_DALIAddressType.Short,
						eCommandPriority := E_DALICommandPriority.Middle,
						nGroup := nGroupAddress);
		IF (NOT fb102AddToGroup.bBusy) THEN
			fb102AddToGroup(bStart := FALSE);
			nStep := 2010;
		END_IF
	ELSIF  (aDeviceGroupState[nAddress] = 2) THEN
		// remove group
		fb102RemoveFromGroup(bStart := TRUE,
							 nAddress := nAddress,
							 eAddressType := E_DALIAddressType.Short,
							 eCommandPriority := E_DALICommandPriority.Middle,
							 nGroup := nGroupAddress);
		IF (NOT fb102RemoveFromGroup.bBusy) THEN
			fb102RemoveFromGroup(bStart := FALSE);
			nStep := 2010;
		END_IF
	ELSE
		nStep := 2010;
	END_IF
2010:
	FOR i := (nAddress + 1) TO 63 DO
		IF (aDeviceGroupState[i] <> 0) THEN
			nNewAddress := TO_BYTE(i);
			nStep := 2000;
			EXIT;
		 END_IF
	END_FOR
	IF (nAddress = nNewAddress) THEN
		nStep := 0;
	ELSE
		nAddress := nNewAddress;
		nStep := 2000;
	END_IF
{endregion}
	ELSE
		nStep := 0;
END_CASE
{endregion}]]></ST>
    </Implementation>
    <LineIds Name="FB_Part102Groups">
      <LineId Id="32" Count="0" />
      <LineId Id="280" Count="7" />
      <LineId Id="279" Count="0" />
      <LineId Id="288" Count="0" />
      <LineId Id="33" Count="3" />
      <LineId Id="310" Count="0" />
      <LineId Id="42" Count="2" />
      <LineId Id="46" Count="1" />
      <LineId Id="466" Count="1" />
      <LineId Id="472" Count="1" />
      <LineId Id="468" Count="1" />
      <LineId Id="465" Count="0" />
      <LineId Id="51" Count="0" />
      <LineId Id="384" Count="0" />
      <LineId Id="382" Count="0" />
      <LineId Id="380" Count="1" />
      <LineId Id="389" Count="0" />
      <LineId Id="405" Count="0" />
      <LineId Id="409" Count="2" />
      <LineId Id="429" Count="0" />
      <LineId Id="412" Count="2" />
      <LineId Id="428" Count="0" />
      <LineId Id="415" Count="2" />
      <LineId Id="427" Count="0" />
      <LineId Id="418" Count="0" />
      <LineId Id="386" Count="0" />
      <LineId Id="383" Count="0" />
      <LineId Id="385" Count="0" />
      <LineId Id="52" Count="0" />
      <LineId Id="573" Count="5" />
      <LineId Id="572" Count="0" />
      <LineId Id="67" Count="0" />
      <LineId Id="69" Count="2" />
      <LineId Id="300" Count="1" />
      <LineId Id="72" Count="1" />
      <LineId Id="271" Count="0" />
      <LineId Id="268" Count="0" />
      <LineId Id="83" Count="1" />
      <LineId Id="240" Count="1" />
      <LineId Id="239" Count="0" />
      <LineId Id="95" Count="0" />
      <LineId Id="97" Count="10" />
      <LineId Id="121" Count="10" />
      <LineId Id="290" Count="1" />
      <LineId Id="302" Count="4" />
      <LineId Id="312" Count="0" />
      <LineId Id="318" Count="1" />
      <LineId Id="322" Count="2" />
      <LineId Id="316" Count="1" />
      <LineId Id="314" Count="0" />
      <LineId Id="307" Count="0" />
      <LineId Id="335" Count="1" />
      <LineId Id="363" Count="16" />
      <LineId Id="340" Count="0" />
      <LineId Id="292" Count="1" />
      <LineId Id="295" Count="1" />
      <LineId Id="485" Count="0" />
      <LineId Id="487" Count="0" />
      <LineId Id="493" Count="4" />
      <LineId Id="488" Count="0" />
      <LineId Id="498" Count="0" />
      <LineId Id="511" Count="0" />
      <LineId Id="499" Count="0" />
      <LineId Id="489" Count="1" />
      <LineId Id="501" Count="4" />
      <LineId Id="507" Count="1" />
      <LineId Id="510" Count="0" />
      <LineId Id="509" Count="0" />
      <LineId Id="512" Count="1" />
      <LineId Id="491" Count="0" />
      <LineId Id="475" Count="0" />
      <LineId Id="522" Count="6" />
      <LineId Id="530" Count="0" />
      <LineId Id="514" Count="0" />
      <LineId Id="533" Count="0" />
      <LineId Id="535" Count="0" />
      <LineId Id="534" Count="0" />
      <LineId Id="532" Count="0" />
      <LineId Id="298" Count="0" />
      <LineId Id="218" Count="3" />
    </LineIds>
  </POU>
</TcPlcObject>